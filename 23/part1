#!/usr/bin/awk -f
BEGIN {
	FS=""
}
function find_dest(value) {
	if (value < 1) {
		return find_dest(ncups)
	} else if (value == pick1 || value == pick2 || value == pick3) {
		return find_dest(value - 1)
	} else {
		return value
	}
}
{
	delete cups
	# the array index is the number, the value points to the next index
	for (i = 1; i < NF; ++i) {
		cups[$i] = $(i + 1)
	}
	cups[$NF] = $1
	ncups = NF
	current = $1
	for (i = 1; i <= 100; ++i) {
		pick1 = cups[current]
		pick2 = cups[pick1]
		pick3 = cups[pick2]
		dest = find_dest(current - 1)
		nx = cups[pick3]
		cups[current] = nx
		current = nx
		cups[pick3] = cups[dest]
		cups[dest] = pick1
	}
	current = cups[1]
	for (i = 2; i <= NF; ++i) {
		printf "%s", current
		current = cups[current]
	}
	printf "\n"
}
